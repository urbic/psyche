PARSER_BEGIN(Parser)

package coneforest.psylla;

class Parser
{
	public Parser(final coneforest.psylla.core.PsyReader oReader)
	{
		this(oReader.reader());
	}
}

PARSER_END(Parser)

SKIP:
{
	" " | "\n" | "\r" | "\t" | "\f" | "\u00A0"
}

TOKEN:
{
	<#SIGN: ["-", "+"]>
}

TOKEN:
{
	<#DIGIT: ["0"-"9"]>
}

TOKEN:
{
	<#HEXDIGIT: <DIGIT> | ["A"-"F", "a"-"f"]>
}

TOKEN:
{
	<#LETTER: ["A"-"Z", "a"-"z"]>
}

TOKEN:
{
	<INTEGRAL:
		<INTEGRAL_DEC>
			| <INTEGRAL_HEX>
			| <INTEGRAL_BIN>
			| <INTEGRAL_OCT>
			| <INTEGRAL_CHR>
	>
}

TOKEN:
{
	<#INTEGRAL_DEC_UNSIGNED: (<DIGIT>)+>
}

TOKEN:
{
	<#INTEGRAL_DEC: (<SIGN>)? <INTEGRAL_DEC_UNSIGNED>>
}

TOKEN:
{
	<#INTEGRAL_HEX: ["X", "x"] "`" (<SIGN>)? (<HEXDIGIT>)+>
}

TOKEN:
{
	<#INTEGRAL_BIN: ["B", "b"] "`" (<SIGN>)? (["0", "1"])+>
}

TOKEN:
{
	<#INTEGRAL_OCT: ["O", "o"] "`" (<SIGN>)? (["0"-"7"])+>
}

TOKEN:
{
	<#INTEGRAL_CHR: ["C", "c"] "`" (~["\\"] | <STRING_SPECIAL>)>
}

TOKEN:
{
	<#REAL_UNSIGNED:
		(
			<INTEGRAL_DEC_UNSIGNED> ("." (<DIGIT>)*)?
			|
			"." <INTEGRAL_DEC_UNSIGNED>
		)
		(<EXPONENT>)?
	>
}

TOKEN:
{
	<REAL: (<SIGN>)? <REAL_UNSIGNED>>
}

TOKEN:
{
	<#EXPONENT: ["E", "e"] <INTEGRAL_DEC>>
}

TOKEN:
{
	<COMMENT: <COMMENT_ONELINE> | <COMMENT_MULTILINE>>
}

TOKEN:
{
	<#COMMENT_ONELINE: "#" (~["\n"])* "\n">
}

TOKEN:
{
	<#COMMENT_MULTILINE: "/#" (~["/"] | ("/" ~["#"]))* "#/">
}

TOKEN:
{
	<STRING: "\"" (<STRING_SPECIAL> | "\\\"" | ~["\"", "\\"])* "\"">
}

TOKEN:
{
	<NAME: <NAME_SLASHED> | <NAME_QUOTED>>
}

TOKEN:
{
	<#NAME_SLASHED: "/" <COMMAND>>
}

TOKEN:
{
	<#NAME_QUOTED: "'" (<STRING_SPECIAL> | "\\'" | ~["'", "\\"])* "'">
}

TOKEN:
{
	<#STRING_SPECIAL:
		"\\"
		(
			["0", "a", "n", "t", "f", "\\", "e", "r", "v", "\n"]
			|
			"u" <HEXDIGIT> <HEXDIGIT> <HEXDIGIT> <HEXDIGIT>
			|
			"x{" (<HEXDIGIT>)+ "}"
			|
			"c" ["\u0000"-"\u00FF"]
		)
	>
}

TOKEN:
{
	<REGEXP:
		"~"
		(
			<STRING_SPECIAL>
			|
			<REGEXP_SPECIAL>
			|
			~["~", "\\"]
		)*
		"~"
	>
}

TOKEN:
{
	<#REGEXP_SPECIAL: "\\" ~[]>
}

TOKEN:
{
	<IMMEDIATE: "/" <NAME_SLASHED>>
}

TOKEN:
{
	<#COMMAND_UNQUALIFIED:
		(<LETTER> | <NAME_SPECIAL_CHAR>)
		(<LETTER> | <NAME_SPECIAL_CHAR> | <DIGIT>)*
		|
		["[", "]", "(", ")", "<", ">", "=", "?"]
	>
}

TOKEN:
{
	<COMMAND: <COMMAND_UNQUALIFIED> ("@" <COMMAND_UNQUALIFIED>)?>
}

TOKEN:
{
	<LITERAL: <COMMAND> "=%" ("\\%" | ~["%", "\\"])* "%">
}

TOKEN:
{
	<OPEN_BRACE: "{">
}

TOKEN:
{
	<CLOSE_BRACE: "}">
}

TOKEN:
{
	<#NAME_SPECIAL_CHAR: ["_", ".", "-", "+", "=", "$"]>
}
